plugins {
  id 'io.spring.dependency-management' version '0.5.7.RELEASE'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id 'com.scuilion.syntastic' version '0.3.6-java7'
}

allprojects {
  repositories {
    jcenter()
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'com.scuilion.syntastic'
  group = 'org.agmip.translators'
  version = '1.0.0-SNAPSHOT'
}

apply plugin: 'maven-publish'

project(':example') {
  apply plugin: 'application'
  apply plugin: 'com.github.johnrengelman.shadow'
  dependencies {
    compile project(':api')
  }
  mainClassName = "org.agmip.examples.translators.ebd.Tester"
}

project(':api') {
  dependencies {
    compile 'org.apache.poi:poi-ooxml:3.15-beta1'
    compile 'com.fasterxml.jackson.core:jackson-core:2.7.5'
    compile 'ch.qos.logback:logback-classic:1.1.7'
  }
  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId 'org.agmip.translators'
        artifactId 'translator-excel-big-data'
        version '1.0.0-SNAPSHOT'
        from components.java
      }
    }
  }
}

task wrapper(type: Wrapper) {
  jarFile = '.gradlew/gradle-wrapper.jar'
}
